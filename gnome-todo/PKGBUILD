# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-todo
pkgver=3.26.0
pkgrel=1
pkgdesc="Task manager for GNOME"
url="https://wiki.gnome.org/Apps/Todo"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server libpeas)
makedepends=(intltool gobject-introspection gnome-common appstream-glib git)
optdepends=('python3: Some plugins')
groups=(gnome-extra)
source=("git+https://git.gnome.org/browse/gnome-todo#branch=gnome-3-26")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_TODO_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson --prefix=/usr ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
