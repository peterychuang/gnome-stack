# $Id: PKGBUILD 292254 2017-04-07 12:16:22Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-weather
pkgver=3.26.0
pkgrel=1
pkgdesc="Access current weather conditions and forecasts"
url="https://wiki.gnome.org/Apps/Weather"
arch=(any)
license=(GPL)
depends=(gtk3 gjs libgweather geoclue2 gnome-desktop)
makedepends=(intltool gobject-introspection autoconf-archive appstream-glib git)
groups=(gnome-extra)
_commit=a9d0dab4c3c44b832275bee20af777d610838ba9  # tags/3.24.0^0
source=("git+https://git.gnome.org/browse/gnome-weather#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
