# $Id: PKGBUILD 298712 2017-06-12 19:22:23Z heftig $
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.26.0
pkgrel=1
pkgdesc="Customize advanced GNOME 3 options"
arch=(any)
url="https://wiki.gnome.org/Apps/GnomeTweakTool"
license=(GPL)
depends=(gnome-settings-daemon python2-gobject)
makedepends=(intltool gnome-common git)
groups=(gnome-extra)
source=("git+https://git.gnome.org/browse/gnome-tweak-tool#branch=gnome-3-26")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson --prefix=/usr ../$pkgname --libexecdir=/usr/lib/ \
    -Dtests=false
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
