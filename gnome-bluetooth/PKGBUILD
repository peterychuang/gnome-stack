# $Id: PKGBUILD 293065 2017-04-17 23:11:42Z heftig $
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=3.25.91+3+ga70bace
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem"
url="https://wiki.gnome.org/Projects/GnomeBluetooth"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gtk3 libnotify bluez libcanberra libsystemd pulseaudio-bluetooth)
makedepends=(intltool gobject-introspection itstool docbook-xsl gnome-common git systemd)
source=("git+https://git.gnome.org/browse/gnome-bluetooth#branch=master"
        61-gnome-bluetooth-rfkill.rules)
sha256sums=('SKIP'
            '8dd2d3637f380a89e72f65e6c8021e994ccbdc645f1a1b1dbffe74ac9f061f53')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOMEBT_V_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson --prefix=/usr --sysconfdir=/etc --localstatedir=/var ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  install -m644 -D ../61-gnome-bluetooth-rfkill.rules \
      "$pkgdir/usr/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
