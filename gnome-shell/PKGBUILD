# $Id: PKGBUILD 295825 2017-05-11 18:19:01Z heftig $
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com

pkgname=gnome-shell
pkgver=3.25.91+33+g5b39afc32
pkgrel=1
pkgdesc="The next generation GNOME Shell"
url="https://wiki.gnome.org/Projects/GnomeShell"
arch=(i686 x86_64)
license=(GPL2)
depends=(accountsservice caribou gcr gjs gnome-bluetooth gnome-menus upower
         gnome-session gnome-settings-daemon gnome-themes-standard gsettings-desktop-schemas
         libcanberra-pulse libcroco libgdm libsecret mutter nm-connection-editor
         telepathy-logger telepathy-mission-control unzip gstreamer)
makedepends=(intltool gtk-doc gnome-control-center evolution-data-server python
             gobject-introspection git gnome-common)
optdepends=('gnome-control-center: System settings'
            'evolution-data-server: Evolution calendar integration')
groups=(gnome)
_commit=fbc5e3709e4a1988aba1acd5c6d9f1f2d126022a  # tags/3.24.2^0
source=("git+https://git.gnome.org/browse/gnome-shell#branch=master"
        "git+https://git.gnome.org/browse/libgnome-volume-control"
        "git+https://git.gnome.org/browse/gnome-shell-sass"
        nm-libexecdir.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'e5bb10ad2e5c3e0fde3d05babd1bfdda701e553e02d493f7e54cb7832ce7e607')

prepare() {
  mkdir build
  cd $pkgname

  git submodule init
  git config --local submodule.src/gvc.url "$srcdir/libgnome-volume-control"
  git config --local submodule.data/theme/gnome-shell-sass.url "$srcdir/gnome-shell-sass"
  git submodule update

  # FS#30747 FS#32730 Problems due to libexecdir different from NM
  patch -Np1 -i ../nm-libexecdir.patch

}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd build
  meson --prefix=/usr --sysconfdir=/etc --buildtype=release ../$pkgname --libexecdir=/usr/lib/gnome-shell \
		 --localstatedir=/var -Dtests=false
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  # Must exist; FS#37412
  mkdir "$pkgdir/usr/share/gnome-shell/modes"
}
