# $Id: PKGBUILD 301553 2017-08-03 13:56:55Z heftig $
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.25.92+1+gf8a57ae0
pkgrel=1
pkgdesc="Single sign-on framework for GNOME"
url="https://wiki.gnome.org/Projects/GnomeOnlineAccounts"
arch=(i686 x86_64)
license=(LGPL)
depends=(webkit2gtk json-glib libnotify rest libsecret telepathy-glib krb5 gcr)
makedepends=(intltool gobject-introspection gtk-doc vala git gnome-common)
optdepends=('gvfs-goa: Virtual file systems (e.g. OwnCloud)'
            'gvfs-google: Google Drive')
_commit=98a3be8021b9474e88a1cabf5b1da27b5b09fa03  # tags/3.24.2^0
source=("git+https://git.gnome.org/browse/gnome-online-accounts#branch=master"
        "git+https://git.gnome.org/browse/telepathy-account-widgets")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.telepathy-account-widgets.url "$srcdir/telepathy-account-widgets"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --enable-media-server \
    --enable-kerberos \
    --enable-lastfm \
    --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
