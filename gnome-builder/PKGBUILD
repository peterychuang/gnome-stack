# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.26.0
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(i686 x86_64)
license=(GPL3)
depends=(gtksourceview3 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils
         ctags libpeas vte3 vala python-jedi autoconf-archive sysprof flatpak gspell)
makedepends=(intltool llvm gobject-introspection gtk-doc yelp-tools appstream-glib vala git
             mm-common)
optdepends=('gnome-code-assistance: Legacy assistance services')
groups=(gnome-extra)
source=("git+https://git.gnome.org/browse/gnome-builder#branch=gnome-builder-3-26"
        "git+https://git.gnome.org/browse/jsonrpc-glib"
        "git+https://git.gnome.org/browse/libdazzle"
        "git+https://git.gnome.org/browse/libgd"
        "git+https://git.gnome.org/browse/template-glib")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname

  git submodule init
  git config --local submodule.jsonrpc-glib.url "subprojects/jsonrpc-glib"
  git config --local submodule.libdazzle.url "subprojects/libdazzle"
  git config --local submodule.libgd.url "subprojects/libgd"
  git config --local submodule.template-glib.url "subprojects/template-glib"
  git submodule update
}

build() {
  cd build
  meson --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}