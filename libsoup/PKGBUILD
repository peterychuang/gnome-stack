# $Id: PKGBUILD 301849 2017-08-10 18:47:44Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup
pkgver=2.59.90.1+3+g4f673b22
pkgrel=1
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2 libxml2 glib-networking sqlite krb5)
makedepends=(intltool gobject-introspection python vala git gtk-doc)
checkdepends=(apache php-apache)
_commit=d059ed12af843def9b5b24a2ea3de69454df8f1f  # tags/2.58.2^0
source=("git+https://git.gnome.org/browse/libsoup#branch=master"
        0001-Configure-PHP-to-load-the-XMLRPC-extension-if-presen.patch)
sha256sums=('SKIP'
            '1ad674810317ff2be817d00e9490d0d70014a38682a3dabc22801ae5db78aecc')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugzilla.gnome.org/show_bug.cgi?id=782410
  patch -Np1 -i ../0001-Configure-PHP-to-load-the-XMLRPC-extension-if-presen.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
