# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=vala
pkgver=0.38.1
pkgrel=1
pkgdesc='Compiler for the GObject type system'
url='https://wiki.gnome.org/Projects/Vala'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2')
makedepends=('libxslt' 'vala')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
source=(git+https://git.gnome.org/browse/vala#branch=0.38)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-vapigen
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
