# $Id: PKGBUILD 299376 2017-06-28 21:55:08Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgtop
pkgver=2.37.90+5+g609b8c7e
pkgrel=1
pkgdesc="A library that reads information about processes and the running system"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2' 'libxau')
makedepends=('gobject-introspection' gnome-common git gtk-doc)
_commit=e0f97e7dbd6ec87103705d3e114876d44cb0ca4c  # gnome-2-36
source=("git+https://git.gnome.org/browse/libgtop#branch=master")
url="http://www.gnome.org/"
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/$pkgname"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-gtk-doc
  make
}

package() {
  cd "${srcdir}/$pkgname"
  make DESTDIR="${pkgdir}" install
}
