# $Id: PKGBUILD 301740 2017-08-07 18:37:25Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.25.92+3+g18ea6a0f9
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(i686 x86_64)
depends=(gnome-online-accounts nss krb5 libgweather libical db libgdata libphonenumber)
makedepends=(intltool gperf gobject-introspection vala gtk-doc boost git cmake ninja)
license=(GPL)
source=("git+https://git.gnome.org/browse/evolution-data-server#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_DATA_SERVER_//;s/_/./g;s/-/+/g'
}

build() {
  cd $pkgname

  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib/$pkgname \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DENABLE_INTROSPECTION=ON \
    -DENABLE_VALA_BINDINGS=ON \
    -DENABLE_UOA=OFF \
    -DENABLE_GOOGLE_AUTH=ON \
    -DENABLE_GTK_DOC=ON \
    -DWITH_PHONENUMBER=ON
  ninja
}

check() {
  cd $pkgname
  # libedata-book tests fail because they try to
  # mkdir /usr/lib/evolution-data-server/camel-providers
  ninja test || :
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
