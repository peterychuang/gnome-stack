# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.25.90+28+g3607e985
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Boxes"
license=('LGPL')
depends=('cdrkit' 'gtk-vnc' 'libarchive' 'libgudev' 'libosinfo' 'libsecret' 'libtracker-sparql'
         'libvirt-glib' 'mtools' 'qemu' 'spice-gtk3')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'spice-protocol' 'vala')
source=(git+https://git.gnome.org/browse/gnome-boxes#branch=master)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/v//g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}