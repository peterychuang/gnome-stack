# $Id: PKGBUILD 293874 2017-04-22 01:02:48Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=3.25.0+47+g16f5112
pkgrel=1
pkgdesc="Take pictures of your screen"
url="https://git.gnome.org/browse/gnome-screenshot"
arch=(i686 x86_64)
license=(GPL2)
depends=(dconf gtk3 libcanberra)
makedepends=(intltool git)
groups=(gnome)
source=("git+https://git.gnome.org/browse/gnome-screenshot#branch=gnome-3-26")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname

  git cherry-pick 07ff6afd07b7fff602d93717dd98d2e7e355592e -n
  git cherry-pick 324d79505f8506c782763d0ca36ed85ad1bc1af3 -n
}

build() {
  cd build
  meson --prefix=/usr ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
