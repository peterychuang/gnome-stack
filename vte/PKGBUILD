# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=vte
pkgver=0.49.1+18+gefbf38e3
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK2"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!emptydirs')
depends=('gtk2' 'vte-common')
makedepends=('pygtk' 'intltool' 'gobject-introspection' 'pygobject2-devel')
url="https://www.gnome.org"
source=(git+https://git.gnome.org/browse/vte#branch=master)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  #warning: type-punning to incomplete type might break strict-aliasing rules
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --disable-gnome-pty-helper
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package(){
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
