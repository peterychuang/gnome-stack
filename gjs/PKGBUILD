# $Id: PKGBUILD 301311 2017-07-28 09:10:47Z jgc $
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.49.91+3+g291fa4e
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(i686 x86_64)
license=(GPL)
depends=(cairo gobject-introspection-runtime js52 gtk3)
makedepends=(gobject-introspection git gnome-common)
_commit=a9db649304db525ca166ec0845ee7a86cea4bf7f  # tags/1.48.6^0
source=("git+https://git.gnome.org/browse/gjs#branch=master"
        d2b083cf3ee74952802f3d8457ee7e24173ba303.patch)
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 < ../d2b083cf3ee74952802f3d8457ee7e24173ba303.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
