# $Id: PKGBUILD 292531 2017-04-08 22:22:28Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.25.90+3+g41247b0
pkgrel=1
pkgdesc="Clocks applications for GNOME"
url="https://wiki.gnome.org/Apps/Clocks"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop geoclue2 geocode-glib gsound)
makedepends=(intltool vala gobject-introspection itstool yelp-tools git)
groups=(gnome-extra)
source=("git+https://git.gnome.org/browse/gnome-clocks#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson --prefix=/usr --sysconfdir=/etc ../$pkgname --localstatedir=/var
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
